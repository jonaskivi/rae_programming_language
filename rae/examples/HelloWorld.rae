module rae.examples.HelloWorld

class Tester
{
public:

	func ()logMe()
	{
		log_s("Hello ")
	}
	
	int data = 2
}

class HelloWorld
{
public:

	//constructor:
	func ()init()
	{
	}
	
	//destructor:
	func ()free()
	{
	}
	
	func ()logIntArray(ref[int] someints)
	{
		for(int a_num in someints)
		{
			log(a_num)
		}
	}

	public func
	()sayHello()
	{
		[int] my_numbers
		my_numbers.emplace_back(1)
		my_numbers.emplace_back(2)
		my_numbers.emplace_back(3)
		my_numbers.emplace_back(4)
		my_numbers.emplace_back(5)
		my_numbers.emplace_back(6)
		my_numbers.emplace_back(7)
		my_numbers.emplace_back(129874234985)
		
		//logIntArray(my_numbers)
		/*
		for(int a_num in my_numbers)
		{
			log(a_num)
		}
		*/
		/*
		for(uint i = 0, i < my_numbers.size, i++)
		{
			log(i, "= ", my_numbers[i])
		}
		*/
		
		tester.logMe
		log("World!")
	}
	
	func (int result) count (int param1, ref Tester param2) public
	{
		if(param2 is null) //TODO oneliner if handling:
			return(0)

		if(param1 > 0 )// TODO is not: and param2 is not null)
		{
			log("whoa.")
		}

		return(param1 + param2.data)
	}
	
	public func (int) number () { return(m_number) }
	public func () number (int set) { m_number = set }
	protected int m_number = 5 // default initializers are copied to constructors.
	
	int no_init_data
	double no_init_datad

	protected int anotherNumber = 42
	float afloat = 248.52

	Tester tester // val is the default type
	opt Tester tester_opt // opt is the optional type. This one is allocated automatically.
	opt Tester tester_not -> null // need to specify that it is null, if you want it to be empty.
	link Tester tester_link // link is a pointer which can not allocate or free memory.
	// a non-owning pointer which is also automatically set to null 
	// when the pointed object is destroyed.
	ptr Tester tester_ptr // Don't use raw pointers unless interfacing with C.

	string name = "Hello Rae World"

	[string] names

	opt[int] opt_array_test
	ref[int] ref_array_test
	ptr[int] ptr_array_test
	//TODO link[int] link_array_test
	[int] array_test //std::vector<int>	
	[int, 5] static_array_test //std::array<int, 5>
	[val Tester] testers	
	[Tester] defaultTesters	
	[link Tester] tester_links	
	[opt Tester] tester_opts	
	[ref Tester] tester_refs
	[ptr Tester] tester_ptrs
}

func (int) main ([string] args)
{
	string helloRae = "What? Hello Rae World"
	log(helloRae)

	val HelloWorld hello; //semicolons are allowed, but not required.

	HelloWorld hello2 //val is default

	log(hello2.name)
	
	//Rae does not use = for pointing to an object. Instead use -> to point.
	//= operator will copy by value, so that the behaviour is consistent,
	//whether you're using pointers or values.
	link Tester tester_lnk
	tester_lnk -> hello.tester

	log("LINK saying hello:")
	tester_lnk.logMe()

	hello.sayHello()

	log_s("5 + 2 = ")

	log(hello.count( hello.number, hello.tester_opt ))

	hello.tester.data = 3

	hello.tester_opts.push_back( hello.tester_opt )

	hello.tester_links.emplace_back
	hello.tester_links[0] -> hello.tester
	log("LINK in an array saying hello: ")
	hello.tester_links[0].logMe()
	log("Ok. Said hello.")
	hello.tester_links[0].data = 8
	log("an eight = ", hello.tester_links[0].data );

	if(true == 1)
	{
		log("true")
	}

	bool isWhat = false
	bool isOther = true

	if(isWhat == true or isOther == true)
	{
		log("false")
	}

	//hello.array_test[0]
	//log("arrays 9 + 2 = ", hello.count( hello.array_test[0], hello.tester_opts[0] ))

	return(0)
}

